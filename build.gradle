plugins {
	id 'fabric-loom' version '1.1-SNAPSHOT' apply false
	id 'io.github.juuxel.loom-quiltflower' version '1.+' apply false
	id 'com.github.johnrengelman.shadow' version '7.1.0' apply false
	id 'org.cadixdev.licenser' version '0.6.1' apply false
	id 'com.modrinth.minotaur' version '2.+' apply false
	id 'com.matthewprenger.cursegradle' version '1.4.0' apply false
}

subprojects {
	apply plugin: 'java'
	apply plugin: 'java-library'
	apply plugin: 'maven-publish'
	apply plugin: 'org.cadixdev.licenser'

	sourceCompatibility = JavaVersion.VERSION_17
	targetCompatibility = JavaVersion.VERSION_17

	version = project.mod_version + "." + getVersionSuffix()

	license {
		header = rootProject.file('HEADER.txt')
	}

	repositories {
		mavenCentral()
		maven { url 'https://jitpack.io' }
		maven { url 'https://repo.codemc.org/repository/maven-public' }
		maven { url 'https://oss.sonatype.org/content/repositories/snapshots' }
	}

	// configure the maven publication
	publishing {
		publications {
			mavenJava(MavenPublication) {
				from components.java
			}
		}

		// See https://docs.gradle.org/current/userguide/publishing_maven.html for information on how to set up publishing.
		repositories {
			// Add repositories to publish to here.
			// Notice: This block does NOT have the same function as the block in the top level.
			// The repositories here will be used for publishing your artifact, not for
			// retrieving dependencies.
		}
	}

	tasks.withType(AbstractArchiveTask) {
		preserveFileTimestamps = false
		reproducibleFileOrder = true
	}

	afterEvaluate {
		if (it.plugins.hasPlugin("com.modrinth.minotaur")) {
			modrinth {
				token = System.getenv("MODRINTH_TOKEN") // This is the default. Remember to have the MODRINTH_TOKEN environment variable set or else this will fail, or set it to whatever you want - just make sure it stays private!
				projectId = "raknetify" // This can be the project ID or the slug. Either will work!
//				versionNumber = project.version + "+" + project.minecraft_version // You don't need to set this manually. Will fail if Modrinth has this version already
//				versionName = project.version + " devbuild for " + project.minecraft_version
				versionType = "alpha" // This is the default -- can also be `beta` or `alpha`
//				uploadFile = remapShadowJar // With Loom, this MUST be set to `remapJar` instead of `jar`!
				gameVersions = ["1.17", "1.17.1", "1.18", "1.18.1", "1.18.2", "1.19", "1.19.1", "1.19.2", "1.19.3"]
//				loaders = ["fabric"]
				changelog = com.ishland.buildscript.ParseGItHubActionChangelog.getChangelog()
			}
		}
	}

}

afterEvaluate {
	logger.lifecycle("Version String: ${project.mod_version + '.' + getVersionSuffix()}")
	logger.lifecycle(com.ishland.buildscript.ParseGItHubActionChangelog.getChangelog())
}

String getVersionSuffix() {
	def stdout = new ByteArrayOutputStream()
	exec {
		commandLine 'git', 'describe', '--tags', '--dirty'
		standardOutput = stdout
	}
	stdout = stdout.toString().strip()
	def suffix = ""
	if (stdout.endsWith("-dirty")) {
		stdout = stdout.substring(0, stdout.length() - "-dirty".length())
		suffix = "-dirty"
	}
	if (stdout.indexOf('-') < 0) {
		return "0" + suffix;
	}
	def split = stdout.split('-')
	return split[split.length - 2] + suffix
}
