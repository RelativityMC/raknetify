evaluationDependsOn(":")

apply plugin: 'fabric-loom'
apply plugin: 'com.github.johnrengelman.shadow'
apply plugin: 'com.modrinth.minotaur'
apply plugin: 'com.matthewprenger.cursegradle'

archivesBaseName = project.archives_base_name + "-fabric"
group = project.maven_group + ".fabric"

configurations {
	shadowInclude
}

dependencies {
	// To change the versions see the gradle.properties file
	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
	modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

	shadowInclude implementation(project(":common"))

	include implementation("com.github.LlamaLad7:MixinExtras:${project.mixinextras_version}")
	annotationProcessor "com.github.LlamaLad7:MixinExtras:${project.mixinextras_version}"

	// Fabric API. This is technically optional, but you probably want it anyway.
//	modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"
}

processResources {
	inputs.property "version", project.version

	filesMatching("fabric.mod.json") {
		expand "version": project.version
	}
}

tasks.withType(JavaCompile).configureEach {
	// Minecraft 1.18 (1.18-pre2) upwards uses Java 17.
	it.options.release = 17
}

java {
	// Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
	// if it is present.
	// If you remove this line, sources will not be generated.
	withSourcesJar()
}

loom {
	accessWidenerPath = file("src/main/resources/raknetify-fabric.accesswidener")
}

jar {
	exclude "META-INF/LICENSE.txt"
	exclude "META-INF/NOTICE.txt"
	from "../LICENSE"
}

shadowJar {
	dependencies {
		exclude(dependency('it.unimi.dsi:fastutil'))
	}
	exclude "META-INF/LICENSE.txt"
	exclude "META-INF/NOTICE.txt"
	minimize()
	archiveClassifier = "all-dev"
	configurations = [ project.configurations.shadowInclude ]
	from "../LICENSE"
}

//noinspection UnnecessaryQualifiedReference
task("remapShadowJar", type: net.fabricmc.loom.task.RemapJarTask, dependsOn: shadowJar) {
	input = shadowJar.archiveFile
	archiveFileName = shadowJar.archiveFileName.get().replaceAll("-dev\\.jar\$", ".jar")
	addNestedDependencies = true
}

assemble.dependsOn(remapShadowJar)

afterEvaluate {
	migrateMappings.configure {
		outputDir = project.file("src/main/java")
	}
}

tasks.withType(AbstractArchiveTask) {
	preserveFileTimestamps = false
	reproducibleFileOrder = true
}

modrinth {
	versionNumber = project.version
	versionName = project.version + " devbuild"
	uploadFile = remapShadowJar
	loaders = ["fabric", "quilt"]
}

if (System.getenv("CURSEFORGE_TOKEN")) {
	curseforge {
		apiKey = System.getenv("CURSEFORGE_TOKEN")
		project {
			id = '631457'
			changelogType = "markdown"
			changelog = com.ishland.buildscript.ParseGItHubActionChangelog.getChangelog()
			releaseType = 'alpha'

			addGameVersion "1.17"
			addGameVersion "1.17.1"
			addGameVersion "1.18"
			addGameVersion "1.18.1"
			addGameVersion "1.18.2"
			addGameVersion "1.19"
			addGameVersion "1.19.1"
			addGameVersion "1.19.2"
			addGameVersion "1.19.3"
			addGameVersion "1.19.4"
			addGameVersion "1.20"
			addGameVersion "1.20.1"
			addGameVersion "1.20.2"
			addGameVersion "1.20.3"
			addGameVersion "1.20.4"
			addGameVersion "1.20.5"
			addGameVersion "1.20.6"
			addGameVersion "Fabric"
			addGameVersion "Java 17"
			addGameVersion "Java 18"
			addGameVersion "Java 19"
			addGameVersion "Java 20"
			addGameVersion "Java 21"
			addGameVersion "Java 22"

			mainArtifact(remapShadowJar) {
				displayName = project.version + " devbuild"
			}
		}
		options {
			forgeGradleIntegration = false
		}
	}
}
